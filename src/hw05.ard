\import Utils
\import hw03
\import lect05

-- 2. Докажите, что (\lam x => not (not x)) = (\lam x => x).

\func not-involutive (x : Bool) : not (not x) = x
  | false => idp
  | true => idp

\func notNotId : (\lam x => not (not x)) = (\lam x => x) =>
  funExt' not-involutive

-- 3. Докажите, что fac не равен suc.

\open Nat(*)

\func fac (n : Nat) : Nat
  | 0 => 1
  | suc n => suc n * fac n

\func fac/=suc (p : fac = suc) : Empty =>
  \let TN (f : Nat -> Nat) : \Type => \case f 3 \with { | 6 => \Sigma | _ => Empty } \in
    repl (pmap TN p) ()

-- 4. Докажите, что left = right, не используя transport, coe и паттерн матчинг с idp.

\func left=right : left = right => \lam i => i

-- 5. Докажите, что если a = a' и b = b', то (a,b) = (a',b'), не используя transport, coe и паттерн матчинг с idp.

\func pairEq {A B : \Type} {a a' : A} {b b' : B} (p : a = a') (q : b = b') : (a,b) = (a',b') =>
  \lam i => (p i, q i)

-- 6. Докажите, что если p = p', то p.1 = p'.1, не используя transport, coe и паттерн матчинг с idp.

\func projEq {A : \Type} (B : A -> \Type) {p p' : \Sigma (x : A) (B x)} (t : p = p') : p.1 = p'.1 =>
  \lam i => (t i).1

-- 7. Докажите pmap2, не используя transport, coe и паттерн матчинг с idp.

\func pmap2' {A B C : \Type} (f : A -> B -> C) {a a' : A} (p : a = a') {b b' : B} (q : b = b') : f a b = f a' b' =>
  \lam i => f (p i) (q i)

-- 8. Мы видели как определить transport через coe. Определите частный случай coe через transport.

\func coe0 (A : I -> \Type) (a : A left) : A right =>
  transport A left=right a

-- 9. Определите функцию B right -> B left.

\func Itr' {B : I -> \Type} (b : B right) : B left =>
  transport B (sym left=right) b
